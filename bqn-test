#!/usr/bin/env bqn

⟨Green, Yellow, Red⟩ ⇐ •Import "../bqn-color/color.bqn"
s ⇐ •Import "../bqn-code/lib/string.bqn"

nl ← @+10

title ← {
    c ← Green‿Yellow‿Red # colors
    d ← "·∘○"            # dots
    nl∾(∾c{𝕎𝕩}¨d)∾Green " bqn-test "∾(∾c{𝕎𝕩}¨○⌽d)∾nl
}

ListRecursiveFiles ← {
    l ← •file.List 𝕩             # files/directories
    P ← (𝕩∾"/")⊸∾                # prefix
    m ← 'd'=•file.Type¨P¨l       # mask
    d ← l/˜m                     # directories
    f ← l/˜((∨´1↓".bqn"⊸⍷)¨l)∧¬m # file
    (P¨f) ∾ ∾𝕊¨P¨d
}

TestSingleFile ← {
    l ← •file.Lines 𝕩                         # lines
    t ← "UnitTest"                            # target
    r ← "UnitTestActual"                      # replace
    m ← s.Unlines t‿r⊸s.Replace¨l             # modded file
    u ← {s.Unwords 1↓s.Words𝕩}¨l/˜{⊑0∾˜t⍷𝕩}¨l # unit tests
    o ← "_temp"
    o •file.Chars ""
    •BQN m
    f   ← 𝕨⊏""‿(𝕩∾" summary: ") # file name
    res ← 'x'=•file.Chars o     # results
    ft  ← (Red¨(nl⊸∾¨u)/˜¬res)  # failing tests
    •file.Remove o
    ∾f∾∾(res⊏⟨Red "○",Green "∘"⟩)∾ft∾((¬𝕨)∧×≠ft)⊏""‿nl
}

IncludesUnitTest ← { ∨´(∨´"UnitTest"⊸⍷)¨(¬·∨´"⇐"⊸⍷)¨⊸/•file.Lines𝕩 }

main ← {
    •Out title
    dir ← 0⊑•args  # TODO add warning for no directory
    v   ← 1<≠•args # verbose # TODO add assert for -v arg
    out ← v⊸TestSingleFile¨∧IncludesUnitTest¨⊸/ListRecursiveFiles dir
    { v ? •Out¨ out ; •Out ∾out }
    •Out ⟨⟩
}

main
